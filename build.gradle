buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
  }
}

plugins {
  id 'com.github.kt3k.coveralls' version '2.6.3'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
  jcenter()
}

group = "com.github.danveloper.ast"
version = "1.0.0-rc-1"

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.4.5'
  testCompile 'cglib:cglib-nodep:3.1'
  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports after running tests.'
    additionalSourceDirs = project.files(sourceSets.main.allSource.srcDirs)
    sourceDirectories = project.files(sourceSets.main.allSource.srcDirs)
    classDirectories =  project.files(sourceSets.main.output)
    reports {
        xml.enabled = true
        csv.enabled = false
        html.enabled = true
    }
}

task sourceJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allJava
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact sourceJar
    }

  }
}

bintrayUpload { task ->
  doFirst {
    if (isSnapshot) {
      throw new GradleException('Cannot publish SNAPSHOT versions to BinTray!')
    }
  }
  gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(task)) {
      task.user = bintrayUser
      task.apiKey = bintrayKey
    }
  }
}

bintray {
  publications = ['maven']
  publish = true
  dryRun = false
  pkg {
    name = "ignore-exceptions-ast"
    userOrg = "danveloper"
    repo = 'maven'
    licenses = ['WTFPL']
    labels = ['groovy']
  }
}
